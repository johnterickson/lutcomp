WHITESPACE = _{" " | "\t"}
alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
label = { ":" ~ alpha ~ (alpha | digit | "_" )+ }
new_line = _{ "\r\n" | "\n"}

unsigned_hex_constant = {ASCII_HEX_DIGIT+}
negation = {"-"}
signed_hex_constant = {negation? ~ unsigned_hex_constant}
char_constant = {"'" ~ ANY ~ "'"}
constant8 = {char_constant | "$" ~ signed_hex_constant }
constant24 = {char_constant | "$" ~ signed_hex_constant | label}
constant32 = {char_constant | "$" ~ signed_hex_constant | label}

register = { "r" ~ unsigned_hex_constant }
deref_register = _{"*" ~ register}

loadimm8 = {"loadimm8" ~ register ~ "<-" ~ constant8 }
copy8 = { "copy8" ~ register ~ "->" ~ register }
jmpreg = {"jmpreg" ~ register }
jmpimm = {"jmpimm" ~ constant24 }
jzimm = {"jzimm" ~ constant24 }
jnegimm = {"jnegimm" ~ constant24 }
load8 = {"load8" ~ deref_register ~ "->" ~ register  }
store8 = {"store8" ~ register ~ "->" ~ deref_register }
ttyin = {"ttyin" ~ register }
ttyout = {"ttyout" ~ register }
push8 = {"push8" ~ register }
pop8 = {"pop8" ~ register }
invert = {"invert" ~ register }
mul8 = { "mul8" ~ register ~ register ~ "->" ~ register }
add8 = { "add8" ~ register ~ register ~ "->" ~ register }
add8nocarry = { "add8nocarry" ~ register ~ register ~ "->" ~ register }
and8 = { "and8" ~ register ~ register ~ "->" ~ register }
andimm8 = { "andimm8" ~ register ~ "<-" ~ constant8 }
orimm8 = { "orimm8" ~ register ~ "<-" ~ constant8 }
xorimm8 = { "xorimm8" ~ register ~ "<-" ~ constant8 }
orimm32 = { "orimm32" ~ register ~ "<-" ~ constant32 }
andimm32 = { "andimm32" ~ register ~ "<-" ~ constant32 }
loadimm32 = { "loadimm32" ~ register ~ "<-" ~ constant32 }
copy32 = { "copy32" ~ register ~ "->" ~ register }
load32 = {"load32" ~ deref_register ~ "->" ~ register  }
store32 = {"store32" ~ deref_register ~ "<-" ~ register }
storeimm32 = {"storeimm32" ~ deref_register ~ "<-" ~ constant32 }
add32nocarryin = { "add32nocarryin" ~ register ~ register ~ "->" ~ register }
add32 = { "add32" ~ register ~ register ~ "->" ~ register }
addimm32ignorecarry = { "addimm32ignorecarry" ~ register ~ "<-" ~ constant32 }
or32 = {"or32" ~ register ~ register ~ "->" ~ register }
and32 = {"and32" ~ register ~ register ~ "->" ~ register }
mul8_16 = {"__mul8_16" ~ register ~ register ~ "->" ~ register }
enable_interrupts = { "enable_interrupts" }
disable_interrupts = { "disable_interrupts" }
io_ready_to_read = { "io_ready_to_read" ~ register }
io_ready_to_write = { "io_ready_to_write" ~ register }
io_in = { "io_in" ~ constant8 ~ register }
io_out = { "io_out" ~ constant8 ~ register }

halt = {"halt" ~ constant32}

op = _{ 
    loadimm8 | copy8 |
    jmpreg | jmpimm | jzimm | jnegimm |
    load8 | store8 | ttyin | ttyout | invert | push8 | pop8 |
    mul8 | add8 | add8nocarry |
    and8 | andimm8 | orimm8 | xorimm8 |
    orimm32 | andimm32 |
    loadimm32 | copy32 |
    load32 | store32 | storeimm32 |
    add32nocarryin | add32 | addimm32ignorecarry |
    or32 | and32 |
    mul8_16 |
    enable_interrupts | disable_interrupts |
    io_ready_to_read | io_ready_to_write | io_in | io_out |
    halt
}

pseudo_call = {"!call" ~ constant24}
pseudo_return = {"!return"}

instruction = {!"#" ~ !":" ~ op }
comment = { "#" ~ (!new_line ~ ANY)* }
literal8 = { "LITERAL8" ~ constant8 }
literal_string_value = { (!new_line ~ ANY)+ }
literal_string = {"LITERAL_STRING" ~ literal_string_value }
image_base_address = { "!image_base_address=(" ~ unsigned_hex_constant ~ ")"}
line = { comment | label | pseudo_call | pseudo_return | instruction | literal8 | literal_string | image_base_address}

program = {SOI ~ (line? ~ new_line)* ~ EOI}