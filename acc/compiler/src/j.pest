WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

lower = { 'a'..'z' }
alpha = { lower | 'A'..'Z' }
digit = { '0'..'9' }
number = { digit+ }
ident = { lower ~ (alpha | digit)* }
ttyin = { "ttyin" }
char_literal_value = { ANY }
char_literal = { "'" ~ char_literal_value ~ "'" }

arithmetic = _{ add | subtract | multiply | or }
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
or = { "||" }

comparison = _{ equals | notequals | greater_than | greater_than_or_equal | less_than | less_than_or_equal}
equals = { "==" }
notequals = { "!=" }
greater_than = { ">" }
greater_than_or_equal = { "@gte" }
less_than = { "<" }
less_than_or_equal = { "@lte" }

comparison_expression = { "(" ~ expression ~ comparison ~ expression ~ ")" }
arithmetic_expression = { "(" ~ expression ~ arithmetic ~ expression ~ ")" }
expression = { char_literal | ttyin | ident | number |  arithmetic_expression | comparison_expression}

else_clause = {"ELSE" ~ "{"~ statement* ~ "}"}
if_else_statement = { "IF" ~ expression ~ "{" ~ statement* ~ "}" ~ else_clause? }
assign = { "ASSIGN" ~ ident ~ ":=" ~ expression ~ ";" }
load = { "LOAD" ~ ident ~ "<-" ~ "*" ~ expression ~ ";"}
store = { "STORE" ~ ident ~ "->" ~ "*" ~ expression ~ ";"}
ttyout = { "TTYOUT" ~ expression ~ ";" }
return_statement = { "RETURN" ~ expression ~ ";" }

call = { "CALL" ~ ident ~ ":=" ~ ident ~ "(" ~  (expression)? ~ ("," ~ expression)* ~ ")" ~ ";" }

statement = { assign | if_else_statement | ttyout | return_statement | call | load | store}
body = { (statement)+ }
paramters = { ident? ~ ("," ~ ident)* }
function = {"FUNCTION " ~ ident ~ "(" ~ paramters ~ ")" ~ "{" ~ body ~ "}"}

program = {
    SOI ~
    function+ ~
    EOI
}