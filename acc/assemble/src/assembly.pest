WHITESPACE = _{" " | "\t"}
alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
label = { ":" ~ alpha ~ (alpha | digit | "_" )+ }
new_line = _{ "\r\n" | "\n"}

unsigned_hex_constant = {ASCII_HEX_DIGIT+}
negation = {"-"}
signed_hex_constant = {negation? ~ unsigned_hex_constant}
char_constant = {"'" ~ ANY ~ "'"}
constant8 = {char_constant | "$" ~ signed_hex_constant }
constant24 = {char_constant | "$" ~ signed_hex_constant | label}
constant32 = {char_constant | "$" ~ signed_hex_constant | label}

register = { "r" ~ unsigned_hex_constant }
deref_register = {"*" ~ register}

loadimm8 = {"loadimm8" ~ register ~ "<-" ~ constant8 }
copy8 = { "copy8" ~ register ~ "->" ~ register }
jmp = {"jmp" ~ constant24 }
jz = {"jz" ~ constant24 }
load8 = {"load8" ~ register ~ "->" ~ deref_register  }
store8 = {"store8" ~ deref_register ~ "->" ~ register }
ttyin = {"ttyin" ~ register }
ttyout = {"ttyout" ~ register }
mul8 = { "mul8" ~ register ~ register ~ "->" ~ register }
add8 = { "add8" ~ register ~ register ~ "->" ~ register }
and8 = { "and8" ~ register ~ register ~ "->" ~ register }
andimm8 = { "andimm8" ~ register ~ "<-" ~ constant8 }
orimm8 = { "orimm8" ~ register ~ "<-" ~ constant8 }
xorimm8 = { "xorimm8" ~ register ~ "<-" ~ constant8 }

orimm32 = { "orimm32" ~ register ~ "<-" ~ constant32 }
andimm32 = { "andimm32" ~ register ~ "<-" ~ constant32 }
loadimm32 = { "loadimm32" ~ register ~ "<-" ~ constant32 }
copy32 = { "copy32" ~ register ~ "->" ~ register }
load32 = {"load32" ~ deref_register ~ "->" ~ register  }
store32 = {"store32" ~ deref_register ~ "->" ~ register }
storeimm32 = {"storeimm32" ~ deref_register ~ "<-" ~ constant32 }
add32nocarryout = { "add32nocarryout" ~ register ~ register ~ "->" ~ register }
add32 = { "add32" ~ register ~ register ~ "->" ~ register }
or32 = {"or32" ~ register ~ register ~ "->" ~ register }
and32 = {"and32" ~ register ~ register ~ "->" ~ register }

halt = {"halt"}

op = _{ 
    loadimm8 | copy8 |
    jmp | jz | 
    load8 | store8 | ttyin | ttyout |
    mul8 | add8 | 
    and8 | andimm8 | orimm8 | xorimm8 |
    orimm32 | andimm32 |
    loadimm32 | copy32 |
    load32 | store32 | storeimm32 |
    add32nocarryout | add32 |
    or32 | and32 |
    halt
}

instruction = {!"#" ~ !":" ~ op }
comment = { "#" ~ (!new_line ~ ANY)* }
line = _{ comment | label | instruction }

program = {SOI ~ (line? ~ new_line)* ~ EOI}