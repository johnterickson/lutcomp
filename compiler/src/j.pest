WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

lower = _{ 'a'..'z' }
alpha = _{ lower | 'A'..'Z' }
digit = { '0'..'9' }
ident_char = _{alpha | digit | "_" }
decimal_number = { digit+ }
hex_number = { "0x" ~ hex_digit+ }
number = { hex_number | decimal_number | char_literal }
hex_digit = _{ digit | 'a'..'f' | 'A'..'F' }
ident = { lower ~ ident_char* }
ttyin = { "ttyin" }
char_literal_value = { ANY }
char_literal = { "'" ~ char_literal_value ~ "'" }

arithmetic = _{ add | subtract | multiply | or | and | left_shift | right_shift }
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
or = { "|" }
and = { "&" }
left_shift = { "<<" }
right_shift = { ">>" }

deref_operator = { "*" }
address_of_operator = {"&"}

comparison = _{ equals | notequals | greater_than_or_equal | greater_than | less_than_or_equal | less_than }
equals = { "==" }
notequals = { "!=" }
greater_than = { ">" }
greater_than_or_equal = { ">=" }
less_than = { "<" }
less_than_or_equal = { "<=" }

RAM_MIN_expression = { "RAM_MIN" }
parentheses_expression = { "(" ~ expression ~ ")" }
comparison_expression = { "(" ~ expression ~ comparison ~ expression ~ ")" }
arithmetic_expression = { "(" ~ expression ~ arithmetic ~ expression ~ ")" }
cast_expression = { "(" ~ expression ~ "AS" ~ variable_type ~ ")" }
deref_expression = { deref_operator ~ expression }
local_field_expression = {ident ~ "." ~ ident}
ptr_field_expression = {ident ~ "->" ~ ident}
call_expression = {ident ~ "(" ~  (expression)? ~ ("," ~ expression)* ~ ")" }
address_of_expression = {address_of_operator ~ expression}
index_expression = {ident ~ "[" ~ expression ~ "]" }
expression = {
    RAM_MIN_expression | number | cast_expression | 
    index_expression | local_field_expression | ptr_field_expression | call_expression |
    ttyin | arithmetic_expression | 
    comparison_expression | deref_expression | address_of_expression | 
    ident | parentheses_expression
    }

pointer_type = { "&" ~ variable_type }
number_type = {"u8" | "usize" | "char" }
array_type = { number_type ~ "[" ~ expression ~ "]" }
variable_type = {array_type | number_type | pointer_type | ident}
variable_decl = {ident ~ ":" ~ variable_type}

else_clause = {"else" ~ "{"~ statement* ~ "}"}
if_else_statement = { "if" ~ expression ~ "{" ~ statement* ~ "}" ~ else_clause? }
declare_statement = { variable_decl ~ ";"}
static_statement = { "static" ~ variable_decl ~ ";"}
while_loop = { "while" ~ expression ~ "{" ~ statement* ~ "}" }
assign_declare = { variable_decl | ident }
assign_deref = { deref_operator ~ ident }
assign_target = { index_expression | local_field_expression | ptr_field_expression | assign_deref | assign_declare }
assign = { assign_target ~ "=" ~ expression ~ ";" }
ttyout = { "ttyout" ~ "(" ~ expression ~ ")" ~ ";" }
return_statement = { "return" ~ expression? ~ ";" }
call_return_value = {ident ~ (":" ~ variable_type)? ~ "="}
void_expression_statement = { expression ~ ";" }

statement = { if_else_statement | ttyout | return_statement | while_loop | assign | static_statement | declare_statement | void_expression_statement}
body = { (statement)+ }
paramters = { variable_decl? ~ ("," ~ variable_decl)* }
function_return_type = {"->" ~ variable_type}
function = {"fn " ~ ident ~ "(" ~ paramters ~ ")" ~ function_return_type? ~ "{" ~ body ~ "}"}

field = { variable_decl }
fields = { (field ~ ";")+ }
struct_decl = {"struct" ~ ident ~ "{" ~ fields ~ "}"}

global = {"global" ~ variable_decl ~ ";"}

include_path = { (!"'" ~ ANY)+ }
include = { "!include" ~ "'" ~ include_path ~ "'" }

image_base_address = { "!image_base_address=(" ~ expression ~ ")"}
statics_base_address = { "!statics_base_address=(" ~ expression ~ ")"}

program = {
    SOI ~
    (image_base_address | statics_base_address | include | struct_decl | function | global)+   ~
    EOI
}