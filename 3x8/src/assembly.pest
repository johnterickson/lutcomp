WHITESPACE = _{" " | "\t"}
alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
label = { ":" ~ alpha ~ (alpha | digit | "_" )+ }
new_line = _{ "\r\n" | "\n"}
hex_constant = {"-"? ~ ASCII_HEX_DIGIT+}
char_constant = {"'" ~ ANY ~ "'"}
constant = {char_constant | hex_constant | label}
register = {
    "acc" | "pc" | "addr" | "mem" |
    "extaddr0" | "extaddr1" | "extaddr2" | "extmem"
}
mode0 = {register ~ register}
mode1 = {constant}
mode2 = {register ~ constant}
inputs = {mode0 | mode2 | mode1}
opcode = {"copy" | "addifzero" | "add" | "or" | "xor" | "and" }
pseudo_call = { "!call" ~ label}
pseudo_return = { "!return" }
pseudo_initstack = { "!init_stack" }
instruction = {!"#" ~ !":" ~ opcode ~ register ~ inputs }
comment = { "#" ~ (!new_line ~ ANY)* }
line = {pseudo_initstack | pseudo_call | pseudo_return | comment | label | instruction }
program = {SOI ~ (line? ~ new_line)* ~ EOI}